plugins {
    id 'java'
}

ext {
    athVersion = '5412.vc75c82cc0c93'
    jenkinsVersion = '2.356' // Last version that supports Java 8
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

group = 'org.jenkins-ci.plugins'
description = 'Acceptance tests of Gradle plugin'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/releases/'
    }
}

dependencies {
    // same version as used by ATH
    annotationProcessor "org.jenkins-ci:annotation-indexer:1.12"

    implementation "org.jenkins-ci:acceptance-test-harness:${athVersion}"
}

def testDependenciesDir = new File("${buildDir}", "test-dependencies")

def copyTestDependencies = tasks.create("copyTestDependencies", Copy) {
    from "${rootProject.buildDir}/libs"
    from "${rootProject.buildDir}/jpi-plugin/generatedJenkinsTest/test-dependencies"

    include '*.hpi'
    include '*.jpi'

    into testDependenciesDir
}

tasks.withType(Test).configureEach {
    dependsOn copyTestDependencies

    environment([
        JENKINS_VERSION: "${jenkinsVersion}",
        PLUGINS_DIR    : testDependenciesDir,
        BROWSER        : 'chrome'
    ])
}
