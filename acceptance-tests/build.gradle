plugins {
    id 'java'
    id 'groovy'
}

ext {
    athVersion = '1.118'
    jenkinsVersion = '2.356' // Last version that supports Java 8
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

group = 'org.jenkins-ci.plugins'
description = 'Acceptance tests of Gradle plugin'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/releases/'
    }
}

dependencies {
    testImplementation "org.jenkins-ci:acceptance-test-harness:${athVersion}"
}

def testDependenciesDir = new File("${buildDir}", "test-dependencies")

def copyTestDependencies = tasks.create("copyTestDependencies", Copy) {
    doFirst {
        mkdir "${buildDir}/target" // FIXME: use groovy script to configure workspace
    }

    from("${rootProject.buildDir}/libs") {
        include '*.hpi'
        include '*.jpi'
    }
    from("${rootProject.buildDir}/jpi-plugin/generatedJenkinsTest/test-dependencies") {
        include '*.hpi'
        include '*.jpi'
    }
    into testDependenciesDir
}

tasks.withType(Test).configureEach {
    dependsOn copyTestDependencies

    environment([
        JENKINS_VERSION: "${jenkinsVersion}",
        PLUGINS_DIR    : testDependenciesDir,
        BROWSER        : 'chrome'
    ])
    systemProperties([
        'user.dir': "$buildDir"
    ])
}
